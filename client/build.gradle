version = '0.1.0'

configurations {
    all*.exclude module: 'apache-jsp'
}

dependencies {
    compileOnly 'com.google.gwt:gwt-user:2.8.0',
            'com.google.gwt:gwt-dev:2.8.0',
            'org.fusesource.restygwt:restygwt:2.2.0'
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    compile 'org.gwtbootstrap3:gwtbootstrap3:0.9.3'
    compile 'org.gwtbootstrap3:gwtbootstrap3-extras:0.9.3'
    compile('com.google.gwt.inject:gin:2.1.2') {
        exclude module: 'asm'
    }
    compile('com.google.inject.extensions:guice-servlet:3.0') {
        exclude module: 'asm'
    }
    compile("org.springframework.boot:spring-boot-starter-web")
    {
        exclude module: "tomcat-embed-jasper"
    }
}

task compileGwt(dependsOn: classes, type: JavaExec) {
    ext.buildDir = "${project.buildDir}/gwt"
    ext.extraDir = "${project.buildDir}/extra"

    inputs.source sourceSets.main.java.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir

    doFirst {
        file(buildDir).mkdirs()
    }

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        [
                sourceSets.main.java.srcDirs,           // Java source
                sourceSets.main.output.resourcesDir,    // Generated resources
                sourceSets.main.output.classesDir,      // Generated classes
                sourceSets.main.compileClasspath,       // Deps
        ]
    }

    args = [
            'org.codetaming.skillsmapper.SkillsMapper',
            '-war', buildDir,
            '-logLevel', 'INFO',
            '-localWorkers', '4',
            '-compileReport',
            '-extra', extraDir,
            '-style', 'OBF',
            '-optimize', '7' // 0=none, 9=max
    ]

    maxHeapSize = '512M'
}

compileJava.dependsOn(processResources)

jar.dependsOn compileGwt
jar {
    baseName = 'skillsmapper-client'
    into('static') {
        from compileGwt.buildDir
    }
}

cloudfoundry {
    space = "development"
    file = file("build/libs/skillsmapper-client-${version}.jar")
    uri = "skills-mapper-client.cfapps.io"
    memory = 512
    instances = 1
}
